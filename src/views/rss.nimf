#? stdtmpl(subsChar = '$', metaChad = '#')
#import strutils, xmltree, strformat, options
#import ../types, ../utils, ../formatters
#
#proc getTitle(tweet: Tweet; prefs: Prefs; retweet: string): string =
#if tweet.pinned: result = "Pinned: "
#elif retweet.len > 0: result = &"RT by @{retweet}: "
#elif tweet.reply.len > 0: result = &"R to @{tweet.reply[0]}: "
#end if
#result &= xmltree.escape(stripHtml(tweet.text))
#if result.len > 0: return
#end if
#if tweet.photos.len > 0:
#  result &= "Image"
#elif tweet.video.isSome:
#  result &= "Video"
#elif tweet.gif.isSome:
#  result &= "Gif"
#end if
#end proc
#
#proc getDescription(desc: string; cfg: Config): string =
Twitter feed for: ${desc}. Generated by ${cfg.hostname}
#end proc
#
#proc renderRssTweet(tweet: Tweet; prefs: Prefs; cfg: Config): string =
#let tweet = tweet.retweet.get(tweet)
#let urlPrefix = getUrlPrefix(cfg)
#let text = replaceUrl(tweet.text, prefs, absolute=urlPrefix)
#if tweet.quote.isSome and get(tweet.quote).available:
#  let quoteLink = getLink(get(tweet.quote))
<p>${text}<br><a href="${urlPrefix}${quoteLink}">${cfg.hostname}${quoteLink}</a></p>
#else:
<p>${text}</p>
#end if
#if tweet.photos.len > 0:
#  for photo in tweet.photos:
<img src="${urlPrefix}${getPicUrl(photo)}" style="max-width:250px;" />
#  end for
#elif tweet.video.isSome:
<img src="${urlPrefix}${getPicUrl(get(tweet.video).thumb)}" style="max-width:250px;" />
#elif tweet.gif.isSome:
#  let thumb = &"{urlPrefix}{getPicUrl(get(tweet.gif).thumb)}"
#  let url = &"{urlPrefix}{getPicUrl(get(tweet.gif).url)}"
<video poster="${thumb}" autoplay muted loop style="max-width:250px;">
  <source src="${url}" type="video/mp4"</source></video>
#end if
#end proc
#
#proc renderRssTweets(tweets: seq[Tweet]; prefs: Prefs; cfg: Config): string =
#let urlPrefix = getUrlPrefix(cfg)
#var links: seq[string]
#for t in tweets:
#  let retweet = if t.retweet.isSome: t.profile.username else: ""
#  let tweet = if retweet.len > 0: t.retweet.get else: t
#  let link = getLink(tweet)
#  if link in links: continue
#  end if
#  links.add link
    <item>
      <title>${getTitle(tweet, prefs, retweet)}</title>
      <dc:creator>@${tweet.profile.username}</dc:creator>
      <description><![CDATA[${renderRssTweet(tweet, prefs, cfg).strip(chars={'\n'})}]]></description>
      <pubDate>${getRfc822Time(tweet)}</pubDate>
      <guid>${urlPrefix & link}</guid>
      <link>${urlPrefix & link}</link>
    </item>
#end for
#end proc
#
#proc renderTimelineRss*(timeline: Timeline; profile: Profile; cfg: Config; multi=false): string =
#let prefs = Prefs(replaceTwitter: cfg.hostname, replaceYouTube: "invidious.snopyta.org")
#let urlPrefix = getUrlPrefix(cfg)
#result = ""
#let user = (if multi: "" else: "@") & profile.username
#var title = profile.fullname
#if not multi: title &= " / " & user
#end if
#title = xmltree.escape(title).sanitizeXml
<?xml version="1.0" encoding="UTF-8"?>
<rss xmlns:atom="http://www.w3.org/2005/Atom" xmlns:dc="http://purl.org/dc/elements/1.1/" version="2.0">
  <channel>
    <atom:link href="${urlPrefix}/${profile.username}/rss" rel="self" type="application/rss+xml" />
    <title>${title}</title>
    <link>${urlPrefix}/${profile.username}</link>
    <description>${getDescription(user, cfg)}</description>
    <language>en-us</language>
    <ttl>40</ttl>
    <image>
      <title>${title}</title>
      <link>${urlPrefix}/${profile.username}</link>
      <url>${urlPrefix}${getPicUrl(profile.getUserPic(style="_400x400"))}</url>
      <width>128</width>
      <height>128</height>
    </image>
#if timeline.content.len > 0:
${renderRssTweets(timeline.content, prefs, cfg)}
#end if
  </channel>
</rss>
#end proc
#
#proc renderListRss*(tweets: seq[Tweet]; list: List; cfg: Config): string =
#let prefs = Prefs(replaceTwitter: cfg.hostname, replaceYouTube: "invidious.snopyta.org")
#let link = &"{getUrlPrefix(cfg)}/{list.username}/lists/{list.name}"
#result = ""
<?xml version="1.0" encoding="UTF-8"?>
<rss xmlns:atom="http://www.w3.org/2005/Atom" xmlns:dc="http://purl.org/dc/elements/1.1/" version="2.0">
  <channel>
    <atom:link href="${link}" rel="self" type="application/rss+xml" />
    <title>${xmltree.escape(list.name)} / @${list.username}</title>
    <link>${link}</link>
    <description>${getDescription(list.name & " by @" & list.username, cfg)}</description>
    <language>en-us</language>
    <ttl>40</ttl>
${renderRssTweets(tweets, prefs, cfg)}
 </channel>
</rss>
#end proc
#
#proc renderSearchRss*(tweets: seq[Tweet]; name, param: string; cfg: Config): string =
#let prefs = Prefs(replaceTwitter: cfg.hostname, replaceYouTube: "invidious.snopyta.org")
#let link = &"{getUrlPrefix(cfg)}/search"
#let escName = xmltree.escape(name)
#result = ""
<?xml version="1.0" encoding="UTF-8"?>
<rss xmlns:atom="http://www.w3.org/2005/Atom" xmlns:dc="http://purl.org/dc/elements/1.1/" version="2.0">
  <channel>
    <atom:link href="${link}" rel="self" type="application/rss+xml" />
    <title>Search results for "${escName}"</title>
    <link>${link}</link>
    <description>${getDescription("Search \"" & escName & "\"", cfg)}</description>
    <language>en-us</language>
    <ttl>40</ttl>
${renderRssTweets(tweets, prefs, cfg)}
  </channel>
</rss>
#end proc
